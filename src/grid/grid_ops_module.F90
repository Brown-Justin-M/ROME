module grid_ops_module
  !/**********************************************************************\
  !| GRID OPS MODULE                                                      |
  !| ---------------                                                      |
  !| This module contains several functions and subroutines designed to   |
  !| operate with the grid_type, including several basic vector calculus  |
  !| operations.                                                          |
  !\**********************************************************************/
  use grid_module, only: dp,li,grid_type
  implicit none
  private
  public div,curl,deriv_x,deriv_y,deriv_z
  public get_shear,get_incline
  public cross,remove_div,remap_array_s,untilt_array_p

contains
  !/**********************************************************************\
  !| FUNCTIONS                                                            |
  !\**********************************************************************/
  function div(grid,time,x)
    !/********************************************************************\
    !| DIV                                                                |
    !| ---                                                                |
    !| Calculates the divergence of the spectral vector x.                |
    !|   grid: a grid_type object that describes the grid                 |
    !|   time: the current time                                           |
    !|   x: a spectral vector array                                       |
    !|   returns: a 3D array of the divergence of x in spectral space     |
    !\********************************************************************/
    implicit none

    type(grid_type), intent(in) :: grid
    real(kind=dp), intent(in) :: time
    complex(kind=dp), intent(in) :: x(:,:,:,:)
    complex(kind=dp) :: div(grid%z_s_count,grid%x_s_count,grid%y_s_count)

    complex(kind=dp), parameter :: iu = (0.0_dp,1.0_dp) 
    real(kind=dp) :: hkx,hky,hkz,incline(2)
    integer :: i,j,k

    incline = get_incline(grid,time)

    do j = 1,grid%y_s_count
      hky = grid%lky(j)
      do i = 1,grid%x_s_count
        hkx = grid%lkx(i)
        do k = 1,grid%z_s_count
          hkz = grid%lkz(k) - hkx * incline(1) - hky * incline(2)
          div(k,i,j) = iu * hkx * x(k,i,j,1)
          div(k,i,j) = div(k,i,j) + iu * hky * x(k,i,j,2)
          div(k,i,j) = div(k,i,j) + iu * hkz * x(k,i,j,3)
        end do
      end do
    end do
  end function

  function get_shear(grid,time)
    !/********************************************************************\
    !| GET_SHEAR                                                          |
    !| ---------                                                          |
    !| Calculate the values du/dz and dv/dz of the background shear at    |
    !| time.                                                              |
    !|   grid: a grid_type object that describes the grid                 |
    !|   time: the current time                                           |
    !|   returns: an array containing (du/dz,dv/dz)                       |
    !\********************************************************************/
    implicit none

    type(grid_type) :: grid
    real(kind=dp), intent(in) :: time
    real(kind=dp) :: get_shear(2)

    real(kind=dp) :: rtmp

    rtmp = time + grid%shear_time_offset

    get_shear(1) = grid%x_shear + grid%x_shear_ramp * rtmp
    get_shear(2) = grid%y_shear + grid%y_shear_ramp * rtmp

    rtmp = grid%shear_freq * (time + grid%shear_time_offset)

    get_shear(1) = get_shear(1) * cos(rtmp)

    rtmp = grid%shear_freq * (time + grid%shear_time_offset) + grid%y_shear_phase

    get_shear(2) = get_shear(2) * cos(rtmp)
  end function

  function get_incline(grid,time)
    !/********************************************************************\
    !| GET_INCLINE                                                        |
    !| -----------                                                        |
    !| Calculate the inclination of the grid at time. Note that this      |
    !| should return the time integral of get_shear.                      |
    !|   grid: a grid_type object that describes the grid                 |
    !|   time: the current time                                           |
    !|   returns: an array containing (x-inclination,y-inclination)       |
    !\********************************************************************/
    implicit none

    type(grid_type) :: grid
    real(kind=dp), intent(in) :: time
    real(kind=dp) :: get_incline(2)

    real(kind=dp) :: rtmp

    rtmp = time + grid%shear_time_offset

    get_incline(1) = (grid%x_shear + grid%x_shear_ramp * rtmp / 2)
    get_incline(2) = (grid%y_shear + grid%y_shear_ramp * rtmp / 2)
    if (grid%shear_freq .ne. 0.0_dp) then
      rtmp = grid%shear_freq * (time + grid%shear_time_offset)
      get_incline(1) = get_incline(1) * sin(rtmp) / grid%shear_freq
      get_incline(1) = get_incline(1) + grid%x_shear_ramp * cos(rtmp) / grid%shear_freq**2

      rtmp = grid%shear_freq * (time + grid%shear_time_offset) + grid%y_shear_phase
      get_incline(2) = get_incline(2) * sin(rtmp) / grid%shear_freq
      get_incline(2) = get_incline(2) + grid%y_shear_ramp * cos(rtmp) / grid%shear_freq**2
    else
      get_incline(1) = get_incline(1) * rtmp
      get_incline(2) = get_incline(2) * rtmp
    end if
    get_incline(1) = get_incline(1) + grid%x_remaps * grid%x_length / grid%z_length
    get_incline(2) = get_incline(2) + grid%y_remaps * grid%y_length / grid%z_length
  end function

  !/**********************************************************************\
  !| SUBROUTINES                                                          |
  !\**********************************************************************/ 
  subroutine curl(grid,time,in,out)
    !/********************************************************************\
    !| CURL                                                               |
    !| ----                                                               |
    !| Calculates the curl of the spectral vector in.                     |
    !|   grid: a grid_type object that describes the grid                 |
    !|   time: the current time                                           |
    !|   in: a spectral vector array                                      |
    !|   out: a 4D array of the curl of in in spectral space              |
    !\********************************************************************/
    implicit none 

    type(grid_type), intent(in) :: grid
    real(kind=dp), intent(in) :: time
    complex(kind=dp), intent(in) :: in(:,:,:,:)
    complex(kind=dp), intent(out) :: out(:,:,:,:)

    complex(kind=dp), parameter :: iu = (0.0_dp,1.0_dp) 
    real(kind=dp) :: hkx,hky,hkz
    real(kind=dp) :: incline(2)
    integer :: i,j,k

    incline = get_incline(grid,time)

    do j = 1,grid%y_s_count
      hky = grid%lky(j)
      do i = 1,grid%x_s_count
        hkx = grid%lkx(i)
        do k = 1,grid%z_s_count
          hkz = grid%lkz(k) - hkx * incline(1) - hky * incline(2)
          out(k,i,j,1) = iu * (hky * in(k,i,j,3) - hkz * in(k,i,j,2))
          out(k,i,j,2) = iu * (hkz * in(k,i,j,1) - hkx * in(k,i,j,3))
          out(k,i,j,3) = iu * (hkx * in(k,i,j,2) - hky * in(k,i,j,1))
        end do
      end do
    end do
  end subroutine

  subroutine deriv_x(grid,in,out)
    !/********************************************************************\
    !| DERIV_X                                                            |
    !| -------                                                            |
    !| Calculates the x derivative of the spectral scalar in.             |
    !|   grid: a grid_type object that describes the grid                 |
    !|   time: the current time                                           |
    !|   in: a spectral scalar array                                      |
    !|   out: a 3D spectral array of d(in)/dx                             |
    !\********************************************************************/
    implicit none

    type(grid_type), intent(in) :: grid
    complex(kind=dp), intent(in) :: in(:,:,:)
    complex(kind=dp), intent(out) :: out(:,:,:)

    complex(kind=dp), parameter :: iu = (0.0_dp,1.0_dp) 
    real(kind=dp) :: hkx
    integer :: i,j

    do j = 1,grid%y_s_count
      do i = 1,grid%x_s_count
        hkx = grid%lkx(i)
        out(:,i,j) = iu * hkx * in(:,i,j)
      end do
    end do
  end subroutine

  subroutine deriv_y(grid,in,out)
    !/********************************************************************\
    !| DERIV_Y                                                            |
    !| -------                                                            |
    !| Calculates the y derivative of the spectral scalar in.             |
    !|   grid: a grid_type object that describes the grid                 |
    !|   time: the current time                                           |
    !|   in: a spectral scalar array                                      |
    !|   out: a 3D spectral array of d(in)/dy                             |
    !\********************************************************************/
    implicit none

    type(grid_type), intent(in) :: grid
    complex(kind=dp), intent(in) :: in(:,:,:)
    complex(kind=dp), intent(out) :: out(:,:,:)

    complex(kind=dp), parameter :: iu = (0.0_dp,1.0_dp) 
    real(kind=dp) :: hky
    integer :: j

    do j = 1,grid%y_s_count
      hky = grid%lky(j)
      out(:,:,j) = iu * hky * in(:,:,j)
    end do
  end subroutine

  subroutine deriv_z(grid,time,in,out)
    !/********************************************************************\
    !| DERIV_Z                                                            |
    !| -------                                                            |
    !| Calculates the z derivative of the spectral scalar in.             |
    !|   grid: a grid_type object that describes the grid                 |
    !|   time: the current time                                           |
    !|   in: a spectral scalar array                                      |
    !|   out: a 3D spectral array of d(in)/dz                             |
    !\********************************************************************/
    implicit none

    type(grid_type), intent(in) :: grid
    real(kind=dp), intent(in) :: time
    complex(kind=dp), intent(in) :: in(:,:,:)
    complex(kind=dp), intent(out) :: out(:,:,:)

    complex(kind=dp), parameter :: iu = (0.0_dp,1.0_dp) 
    real(kind=dp) :: hkx,hky,hkz,incline(2)
    integer :: i,j,k

    incline = get_incline(grid,time)

    do j = 1,grid%y_s_count
      hky = grid%lky(j)
      do i = 1,grid%x_s_count
        hkx = grid%lkx(i)
        do k = 1,grid%z_s_count
          hkz = grid%lkz(k) - hkx * incline(1) - hky * incline(2)
          out(k,i,j) = iu * hkz * in(k,i,j)
        end do
      end do
    end do
  end subroutine

  subroutine cross(a,b,out,dim)
    !/********************************************************************\
    !| CROSS                                                              |
    !| -----                                                              |
    !| Calculates one component of the cross product of a with b.         |
    !|   a: a spectral scalar array                                       |
    !|   b: a spectral scalar array                                       |
    !|   dim: the desired component of the cross product (1 for x, etc.)  |
    !|   out: a 3D spectral array of the component of the cross product   |
    !\********************************************************************/
    implicit none

    real(kind=dp), pointer, intent(in) :: a(:,:,:,:)
    real(kind=dp), pointer, intent(in) :: b(:,:,:,:)
    real(kind=dp), pointer, intent(out) :: out(:,:,:)
    integer, intent(in) :: dim

    select case (dim)
    case (1)
      out = a(:,:,:,2) * b(:,:,:,3) - a(:,:,:,3) * b(:,:,:,2)
    case (2)
      out = a(:,:,:,3) * b(:,:,:,1) - a(:,:,:,1) * b(:,:,:,3)
    case (3)
      out = a(:,:,:,1) * b(:,:,:,2) - a(:,:,:,2) * b(:,:,:,1)
    end select
  end subroutine

  subroutine remove_div(grid,time,in,x,factor)
    !/********************************************************************\
    !| REMOVE_DIV                                                         |
    !| ----------                                                         |
    !| Removes divergence by solving the following for x                  |
    !|       laplace(p) = div(in)                                         |
    !|     factor * x = factor * x - grad(p) - (in - factor * x)          |
    !| Often in incompressible systems, it is sufficient to solve the     |
    !| pressure such that the rhs of the system is divergence free. This  |
    !| is not the case if the coordinate system is changing with time. In |
    !| that case, the velocity update equation is expected to look like   |
    !|     u = in + factor * x,                                           |
    !| where "factor" is the multiplier on the rhs of the current time,   |
    !| "x" is the divergence-removed rhs, and "in" is the estimate of the |
    !| velocity (accounting for any prior steps in a multi-step method).  |
    !|   grid: a grid_type object that describes the grid                 |
    !|   time: the current time                                           |
    !|   in: a 4D array containing the estimate of the velocity           |
    !|   x: a 4D array for the quantity from which to remove divergence   |
    !|   factor: the factor on x, if omitted, defaults to 1               |
    !\********************************************************************/
    implicit none

    type(grid_type), intent(in) :: grid
    real(kind=dp), intent(in) :: time
    complex(kind=dp), intent(in) :: in(:,:,:,:)
    complex(kind=dp), intent(inout) :: x(:,:,:,:)
    real(kind=dp), intent(in), optional :: factor

    real(kind=dp) :: factorin

    complex(kind=dp), parameter :: iu = (0.0_dp,1.0_dp) 
    real(kind=dp) :: hkx,hky,hkz,ksquared
    real(kind=dp) :: incline(2)
    integer :: i,j,k
    complex(kind=dp) :: ctmp

    factorin = 1.0_dp
    if (present(factor)) factorin = factor

    incline = get_incline(grid,time)

    do j = 1,grid%y_s_count
      hky = grid%lky(j)
      do i = 1,grid%x_s_count
        hkx = grid%lkx(i)
        do k = 1,grid%z_s_count
          hkz = grid%lkz(k) - hkx * incline(1) - hky * incline(2)
          ksquared = max(hkx**2 + hky**2 + hkz**2,epsilon(1.0_dp))

          ctmp = hkx * in(k,i,j,1)
          ctmp = ctmp + hky * in(k,i,j,2)
          ctmp = ctmp + hkz * in(k,i,j,3)

          ctmp = ctmp / ksquared / factorin

          x(k,i,j,1) = x(k,i,j,1) - hkx * ctmp
          x(k,i,j,2) = x(k,i,j,2) - hky * ctmp
          x(k,i,j,3) = x(k,i,j,3) - hkz * ctmp
        end do
      end do
    end do
  end subroutine

  subroutine remap_array_s(grid,time,x)
    !/********************************************************************\
    !| REMAP_ARRAY_S                                                      |
    !| -------------                                                      |
    !| Remaps the array x by one step to reduce the inclination of the    |
    !| array. This also uses the Delorme dealiasing method, see below, to |
    !| avoid any aliasing that could occur during the remap. For more     |
    !| details, see Rogallo (1977,1981).                                  |
    !|   grid: a grid_type object that describes the grid                 |
    !|   time: the current time                                           |
    !|   x: a spectral scalar array to remap                              |
    !\********************************************************************/
    use transform_module, only : transform_z_p_to_s,transform_z_s_to_p
    implicit none

    type(grid_type), intent(in) :: grid
    real(kind=dp), intent(in) :: time
    complex(kind=dp), intent(inout) :: x(0:,grid%x_s_offset:,grid%y_s_offset:)
    
    complex(kind=dp), parameter :: iu = (0.0_dp,1.0_dp)
    real(kind=dp) :: shift,incline(2)
    complex(kind=dp) :: cwork(grid%z_n,grid%x_s_count,grid%y_s_count)
    integer :: i,j,k,x_sign,y_sign

    incline = get_incline(grid,time)

    ! Check if the grid is sheared too far in x
    x_sign = 0
    if (incline(1) * grid%z_length .gt. grid%x_length / 2.0_dp) then
      x_sign = 1
    else if (incline(1) * grid%z_length .lt. -grid%x_length / 2.0_dp) then
      x_sign = -1
    end if

    ! Check if the grid is sheared too far in y
    y_sign = 0
    if (incline(2) * grid%z_length .gt. grid%y_length / 2.0_dp) then
      y_sign = 1
    else if (incline(2) * grid%z_length .lt. -grid%y_length / 2.0_dp) then
      y_sign = -1
    end if

    ! Dealias before the remap
    if (x_sign .ne. 0) call x_delorme_dealias(grid,x)
    if (y_sign .ne. 0) call y_delorme_dealias(grid,x,y_sign)

    call transform_z_s_to_p(x,cwork(:,:,:))

    ! Remap in x, with the data partially transformed
    if (x_sign .ne. 0) then
      do j = 1,grid%y_s_count
        do i = 1,grid%x_s_count
          do k = 1,grid%z_n
            shift = x_sign * grid%x_length / grid%z_length * grid%z(k)
            cwork(k,i,j) = cwork(k,i,j) * exp(-iu * grid%lkx(i) * shift)
          end do
        end do
      end do
    end if

    ! Remap in y, with the data partially transformed
    if (y_sign .ne. 0) then
      do k=1,grid%z_n
        do j = 1,grid%y_s_count
          do i = 1,grid%x_s_count
            shift = y_sign * grid%y_length / grid%z_length * grid%z(k)
            cwork(k,i,j) = cwork(k,i,j) * exp(-iu * grid%lky(j) * shift)
          end do
        end do
      end do
    end if

    call transform_z_p_to_s(cwork(:,:,:),x)

    ! Dealias after the remap
    if (x_sign .ne. 0) call x_delorme_dealias(grid,x)
    if (y_sign .ne. 0) call y_delorme_dealias(grid,x,y_sign)
  end subroutine

  subroutine untilt_array_p(grid,time,in,out)
    !/********************************************************************\
    !| UNTILT_ARRAY_P                                                     |
    !| -------------                                                      |
    !| Generates in out an array that shows the array in but transformed  |
    !| into Cartesian space.                                              |
    !|   grid: a grid_type object that describes the grid                 |
    !|   time: the current time                                           |
    !|   in: a 3D physical scalar array containing the array to tranform  |
    !|   out: a 3D physical scalar array containing the transformed array |
    !\********************************************************************/
    use grid_module, only: allocate_s
    use transform_module, only: transform_xy_s_to_p,transform_xy_p_to_s
    implicit none

    type(grid_type), intent(in) :: grid
    real(kind=dp), intent(in) :: time
    real(kind=dp), intent(in) :: in(:,:,:)
    real(kind=dp), intent(out) :: out(:,:,:)

    complex(kind=dp),parameter :: iu = (0.0_dp,1.0_dp)
    complex(kind=dp) :: cwork(grid%z_n,grid%x_s_count,grid%y_s_count)
    real(kind=dp) :: shift,incline(2)
    integer :: i,j,k

    incline = get_incline(grid,time)

    call transform_xy_p_to_s(in,cwork)

    do j = 1,grid%y_s_count
      do i = 1,grid%x_s_count
        do k = 1,grid%z_n
          shift = incline(1) * grid%z(k)
          cwork(k,i,j) = cwork(k,i,j) * exp(-iu * grid%lkx(i) * shift)
          shift = incline(2) * grid%z(k)
          cwork(k,i,j) = cwork(k,i,j) * exp(-iu * grid%lky(j) * shift)
        end do
      end do
    end do

    call transform_xy_s_to_p(cwork,out)
  end subroutine

  subroutine x_delorme_dealias(grid,x)
    !/********************************************************************\
    !| X_DELORME_DEALIAS                                                  |
    !| -----------------                                                  |
    !| Dealias the array x by setting the modes satisfying                |
    !|    kz > -kx + max(k), or                                           |
    !|    kz < kx + min(k),                                               |
    !| which is outlined in detail in Delorme (1985) as necessary for a   |
    !| remapping in the x direction.                                      |
    !|   grid: a grid_type object that describes the grid                 |
    !|   x: the spectral scalar array to dealias                          |
    !\********************************************************************/
    implicit none

    type(grid_type), intent(in) :: grid
    complex(kind=dp), intent(inout) :: x(:,:,:)

    integer :: i,j,k

    grid%xz_delorme_mask = 1.0_dp

    ! Set the dealiasing mask
    do i = 1,grid%x_s_count
      do k = 1,grid%z_s_count
        if (grid%lkz(k) .gt. -grid%lkx(i) + maxval(grid%kz)) then
          grid%xz_delorme_mask(k,i) = 0.0_dp
        end if
        if (grid%lkz(k) .lt. grid%lkx(i) + minval(grid%kz)) then
          grid%xz_delorme_mask(k,i) = 0.0_dp
        end if
      end do
    end do

    ! Apply the dealiasing mask
    do j = 1,grid%y_s_count
      x(:,:,j) = grid%xz_delorme_mask(:,:) * x(:,:,j)
    end do
  end subroutine

  ! TODO: this needs to be tested
  subroutine y_delorme_dealias(grid,x,sign)
    !/********************************************************************\
    !| Y_DELORME_DEALIAS                                                  |
    !| -----------------                                                  |
    !| Dealias the array x by setting the modes satisfying                |
    !|    kz > sign * ky + max(k), or                                     |
    !|    kz < sign * ky + min(k),                                        |
    !| which is outlined in detail in Delorme (1985) as necessary for a   |
    !| remapping in the y direction.                                      |
    !|   grid: a grid_type object that describes the grid                 |
    !|   x: the spectral scalar array to dealias                          |
    !|   sign: +1 if the initial array is tilted towards +y, -1 for -y    |
    !\********************************************************************/
    implicit none

    type(grid_type) :: grid
    complex(kind=dp) :: x(:,:,:)
    integer :: sign

    integer :: i,j,k

    grid%yz_delorme_mask = 1.0_dp

    ! Set the dealiasing mask
    do j = 1,grid%y_s_count
      do k = 1,grid%z_s_count
        if (grid%lkz(k) .gt. sign * grid%lky(j) + maxval(grid%kz)) then
          grid%yz_delorme_mask(k,j) = 0.0_dp
        end if
        if (grid%lkz(k) .lt. sign * grid%lky(j) + minval(grid%kz)) then
          grid%yz_delorme_mask(k,j) = 0.0_dp
        end if
      end do
    end do

    ! Apply the dealiasing mask
    do j = 1,grid%y_s_count
      do i = 1,grid%x_s_count
        x(:,i,j) = grid%yz_delorme_mask(:,j) * x(:,i,j)
      end do
    end do
  end subroutine

end module grid_ops_module